pragma options "-V10 --beopt:simiters 100 --fe-trunc-var-arr --spmd-max-nproc 4 --bnd-inbits 3 --bnd-cbits 2 --bnd-unroll-amnt 4 --bnd-inline-amnt 5 --bnd-arr1d-size 4 --bnd-arr-size 4 --be:ufunSymmetry --beopt:simplifycex NOSIM"; //  --be:showDAG

// BUG: when use FFIELD, there is a cex: P=1, r[0]==7, and EQ 7 == 0 will fail!

package spmd;
// NOTE: how to refer to spmd::some_var if I am not in spmd package?

int SPMD_MAX_NPROC = 4;
int SPMD_MAX_BUF_LEN = 2;
include "spmd.skh";

include "boundedgen.skh";

double movein(global double[spmdnproc] ga) {
	return ga[spmdpid];
}

void moveout(ref global double[spmdnproc] ga, double local) {
	ga[spmdpid] = local;
}

struct LSingle {
	double val;
}

LSingle movein_single(global double[spmdnproc] gv) {
	LSingle s = new LSingle(val=gv[spmdpid]);
	return s;
}

void moveout_single(ref global double[spmdnproc] gv, LSingle s) {
	gv[spmdpid] = s.val;
}

bit powerOf2(int n) {
	if (n > 0) {
		while (n > 1) {
			if (n % 2 != 0) {
				return false;
			}
			n = n / 2;
		}
		return true;
	} else {
		return false;
	}
}

// NOTE: how to assert that value is on every processor? now the double[P] result seems clumsy!
void spec(global int P, global double[P] r, ref global double[P] result) {
	assume powerOf2(P);
	assume P <= SPMD_MAX_NPROC;
	double sum = 0.0;
	for (int i=0; i<P; i++) {
		sum = sum + r[i];
	}
	for (int p=0; p<P; p++) {
		result[p] = sum;
	}
}

int setup_l2npcols() {
	global int n = spmdnproc/2;
	global int l2npcols = 0;
	while (n > 0) {
		l2npcols++;
		n = n /2;
	}
	return l2npcols;
}

void setup_reduce(int l2npcols, ref int[l2npcols] arr) {
	int div_factor = spmdnproc;
	for (int i=0; i<l2npcols; i++) {
		//int j = (spmdpid + div_factor/2) % div_factor + (spmdpid/div_factor)*div_factor;
		int ops_usage;
		int j = boundedexpr(3, {spmdpid, div_factor, div_factor/2}, 4, {PLUS, TIMES, DIV, MOD}, ops_usage);	//Error: AN INTEGER GOT REALLY BIG, AND IS NOW BEYOND THE SCOPE OF THE SOLVER
		minimize(ops_usage);
		arr[i] = j;
		div_factor = div_factor/2;
	}
}

double allreduce(global int l2npcols, int[l2npcols] reduce_exch_proc, double x) {
	double[1] sum;
	sum[0] = x;
	double[1] buf;
	for (global int i=0; i<l2npcols; i++) {
		int recipient = reduce_exch_proc[i];
		//spmdtransfer(global int size, bit scond, double [size] sendbuf, int recipient, bit rcond, ref double [size] recvbuf)
		spmdtransfer(1, 1, sum, recipient, 1, buf);
		sum[0] = sum[0] + buf[0];
	}
	return sum[0];
}

void imp(int P, double[P] r, ref global double[P] result) implements spec {
	spmdfork(P) {
		double x = movein(r);
		LSingle res = movein_single(result);

		global int l2npcols = setup_l2npcols();
		int[l2npcols] reduce_exch_proc;
		setup_reduce(l2npcols, reduce_exch_proc);

		res.val = allreduce(l2npcols, reduce_exch_proc, x);
		
		moveout_single(result, res);
	}
}

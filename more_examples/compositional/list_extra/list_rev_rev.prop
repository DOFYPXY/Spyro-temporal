//@Description 

var {
    list l;
    list rev1_out;
    list rev2_out;

    boolean err;
}

relation {
    reverse(l, rev1_out);
    reverse(rev1_out, rev2_out);
}

generator {
    boolean AP -> !GUARD || RHS;
    boolean GUARD -> true | is_empty(l) | !is_empty(l);
    boolean RHS -> ERR | equal_list(rev2_out, L);
    boolean ERR -> err;
    list LV -> l | rev1_out | rev2_out;
    list L1 -> tail(l, err) | nil();
    list L2 -> tail(L1, err);
    list L -> LV | L1 | L2;
}

example {
    int -> ??(3) | -1 * ??(3) ;
    list -> nil() | cons(int, list);
    boolean -> true | false;
}

struct list {
    int hd;
	list tl;
}

void nil(ref list ret) {
    ret = null;
}

void cons(int hd, list tl, ref list ret) {
    ret = new list();
    ret.hd = hd;
    ret.tl = tl;
}

void head(list l, ref boolean err, ref int ret) {
    if (l == null) {
        err = true;
        ret = 0;
    } else {
        err = false;
        ret = l.hd;
    }
}

void tail(list l, ref boolean err, ref list ret) {
    if (l == null) {
        err = true;
        ret = null;
    } else {
        err = false;
        ret = l.tl;
    }
}

void list_copy(list l, ref list ret) {
    if (l == null) {
        ret = null;
    } else {
        ret = new list();
        ret.hd = l.hd;

        list tl_copy;
        list_copy(l.tl, tl_copy);
        ret.tl = tl_copy;
    } 
}

void snoc(list l, int val, ref list ret) {
    if (l == null) {
        ret = new list();
        ret.hd = val;
        ret.tl = null;
    } else {
        ret = new list();
        ret.hd = l.hd;
        snoc(l.tl, val, ret.tl);
    }
}

void reverse(list l, ref list ret) {
    if (l == null) {
        ret = null;
    } else {
        list tl_reverse;
        reverse(l.tl, tl_reverse);
        snoc(tl_reverse, l.hd, ret);
    }
}

void len(list l, ref int ret) {
    if (l == null) {
        ret = 0;
    } else {
        len(l.tl, ret);
        ret = ret + 1;
    }
}

void is_empty(list l, ref boolean ret) {
    ret = (l == null);
}

void equal_list(list l1, list l2, ref boolean ret) {
    if (l1 == null || l2 == null) {
        ret = l1 == l2;
    } else {
        equal_list(l1.tl, l2.tl, ret);
        ret = l1.hd == l2.hd && ret;
    }
}
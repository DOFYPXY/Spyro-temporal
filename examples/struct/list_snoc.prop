//@Description 

var {
    list snoc_in_list;
    int snoc_in_val;
    list snoc_out_list;
}

relation {
    snoc(snoc_in_list, snoc_in_val, snoc_out_list);
}

generator {
    boolean AP -> SAP | is_empty(L) | !is_empty(L) | equal_list(L, L) | !equal_list(L, L);
    boolean SAP -> S < S + S + ??(1) | S == S + S + ??(1) | S > S + S + ??(1) | S != S + S + ??(1) ;
    int S -> len(L) | 0 ;
    list L -> snoc_in_list | snoc_out_list ;
}

example {
    int -> ?? | -1 * ?? ;
    boolean -> ?? ;
    list -> nil() | cons(int, list);
}

struct list {
    int hd;
	list tl;
}

void nil(ref list ret) {
    ret = null;
}

void cons(int hd, list tl, ref list ret) {
    ret = new list();
    ret.hd = hd;
    ret.tl = tl;
}

void head(list l, ref int ret) {
    assert (l != null);

    ret = l.hd;
}

void tail(list l, ref list ret) {
    assert (l != null);

    ret = l.tl;
}

void list_copy(list l, ref list ret) {
    if (l == null) {
        ret = null;
    } else {
        ret = new list();
        ret.hd = l.hd;

        list tl_copy;
        list_copy(l.tl, tl_copy);
        ret.tl = tl_copy;
    } 
}

void snoc(list l, int val, ref list ret) {
    if (l == null) {
        ret = new list();

        ret.hd = val;
        ret.tl = null;
    } else {
        list tl;
        snoc(l.tl, val, tl);
        
        ret = new list();
        ret.hd = l.hd;
        ret.tl = tl;    
    }
}

void len(list l, ref int ret) {
    if (l == null) {
        ret = 0;
    } else {
        len(l.tl, ret);
        ret = ret + 1;
    }
}

void is_empty(list l, ref boolean ret) {
    ret = (l == null);
}

void equal_list(list l1, list l2, ref boolean ret) {
    if (l1 == null || l2 == null) {
        ret = l1 == l2;
    } else {
        equal_list(l1.tl, l2.tl, ret);
        ret = l1.hd == l2.hd && ret;
    }
}
//@Description 

var {
    list append_l1;
    list append_l2;
    list append_out;
}

relation {
    append(append_l1, append_l2, append_out);
}

generator {
    boolean AP -> SAP | is_empty(L) | !is_empty(L) | equal_list(L, L) | !equal_list(L, L);
    boolean SAP -> S < S + S + ??(1) | S == S + S + ??(1) | S > S + S + ??(1) | S != S + S + ??(1) ;
    int S -> len(L) | 0 ;
    list L -> append_l1 | append_l2 | append_out ;
}

example {
    int -> ?? | -1 * ?? ;
    boolean -> ?? ;
    list -> nil() | cons(int, list);
}

struct list {
    int hd;
	list tl;
}

void nil(ref list ret) {
    ret = null;
}

void cons(int hd, list tl, ref list ret) {
    ret = new list();
    ret.hd = hd;
    ret.tl = tl;
}

void head(list l, ref int ret) {
    assert (l != null);

    ret = l.hd;
}

void tail(list l, ref list ret) {
    assert (l != null);

    ret = l.tl;
}

void list_copy(list l, ref list ret) {
    if (l == null) {
        ret = null;
    } else {
        ret = new list();
        ret.hd = l.hd;

        list tl_copy;
        list_copy(l.tl, tl_copy);
        ret.tl = tl_copy;
    } 
}

void append(list l1, list l2, ref list ret) {
    if (l1 == null) {
        list_copy(l2, ret);
    } else {
        list tl_append;
        append(l1.tl, l2, tl_append);
        cons(l1.hd, tl_append, ret);
    }
}

void len(list l, ref int ret) {
    if (l == null) {
        ret = 0;
    } else {
        len(l.tl, ret);
        ret = ret + 1;
    }
}

void is_empty(list l, ref boolean ret) {
    ret = (l == null);
}

void equal_list(list l1, list l2, ref boolean ret) {
    if (l1 == null || l2 == null) {
        ret = l1 == l2;
    } else {
        equal_list(l1.tl, l2.tl, ret);
        ret = l1.hd == l2.hd && ret;
    }
}
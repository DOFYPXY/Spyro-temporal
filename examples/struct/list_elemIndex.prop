//@Description need inline bnd 10

var {
    list elemIndex_in_list;
    int elemIndex_in_val;
    int elemIndex_out_val;
}

relation {
    elemIndex(elemIndex_in_list, elemIndex_in_val, elemIndex_out_val);
}

generator {
    boolean AP -> SAP | U | E | is_empty(L) | !is_empty(L);
    boolean U -> forall((x) -> x <= I, L) | forall((x) -> x == I, L) | forall((x) -> x >= I, L) | forall((x) -> x != I, L)  ;
    boolean E -> exists((x) -> x <= I, L) | exists((x) -> x == I, L) | exists((x) -> x >= I, L) | exists((x) -> x != I, L)  ;
    boolean SAP -> S < S + ??(1) | S == S + ??(1) | S > S + ??(1) | S != S + ??(1) ;
    list L -> elemIndex_in_list ;
    int S -> elemIndex_out_val | len(L) | 0 | -1 ;
    int I -> elemIndex_in_val ;
}

example {
    int -> ??(3) | -1 * ??(3) ;
    boolean -> ?? ;
    list -> nil() | cons(int, list);
}

struct list {
    int hd;
	list tl;
}

void nil(ref list ret) {
    ret = null;
}

void cons(int hd, list tl, ref list ret) {
    ret = new list();
    ret.hd = hd;
    ret.tl = tl;
}

void head(list l, ref int ret) {
    assert (l != null);

    ret = l.hd;
}

void tail(list l, ref list ret) {
    assert (l != null);

    ret = l.tl;
}

void elemIndex(list l, int val, ref int ret) {
    if (l == null) {
        ret = -1;
    } else if (val == l.hd) {
        ret = 0;
    } else {
        elemIndex(l.tl, val, ret);
        if (ret >= 0) { ret = ret + 1; }
    }
}

void forall(fun f, list l, ref boolean ret) {
    if (l == null) {
        ret = true;
    } else {
        forall(f, l.tl, ret);        
        ret = ret && f(l.hd);
    }
}

void exists(fun f, list l, ref boolean ret) {
    if (l == null) {
        ret = false;
    } else {
        exists(f, l.tl, ret);
        ret = ret || f(l.hd);
    }
}

void is_empty(list l, ref boolean ret) {
    ret = (l == null);
}

void len(list l, ref int ret) {
    if (l == null) {
        ret = 0;
    } else {
        len(l.tl, ret);
        ret = ret + 1;
    }
}

void equal_list(list l1, list l2, ref boolean ret) {
    if (l1 == null || l2 == null) {
        ret = l1 == l2;
    } else {
        equal_list(l1.tl, l2.tl, ret);
        ret = l1.hd == l2.hd && ret;
    }
}
//@Description This sketch synthesizes the Church encodings for AND, NOT, and OR constructs.

/**
 * Command line arguments that help control the depth of inlining for recursive functions. 
 * For this benchmark, the test harnesses only check 3-bit integers for 
 * each of the 20 integer input values.
 */
pragma options "--bnd-inbits 3 --bnd-inline-amnt 3";

/**
 * Data definitions
 */
adt srcAST{
	VarS { int name;}
	AbsS { VarS var; srcAST a;}
	AppS { srcAST a; srcAST b;}
	AndS { srcAST a; srcAST b;}
	OrS { srcAST a; srcAST b;}
	NotS { srcAST a;}
	TrueS {}
	FalseS {}
}

adt dstAST {
	VarD { int name;}
	AbsD { VarD var; dstAST a;}
	AppD { dstAST a; dstAST b;}
}	

/**
 * Function to synthesize
 */
dstAST desugar(srcAST s){ 
	if(s == null) return null; 
		
	generator VarD getVar(VarS var) {
		if (var == null) return new VarD(name = ??);
		else return new VarD(name = {|var.name | ?? |});
	}
	
	/**
	 * This generator produces a destination AST with maximum depth bnd that uses dstAST nodes c1 and c2 
	 */
	generator dstAST getPart(dstAST c1, dstAST c2, int bnd) {
		dstAST z = new VarD(name = ??);
		//This is one of the tricks used to scale this benchmark. The encodings for True and False are used repeatedly
		// in the desugaring. Hence, by making getPart use them directly, we can reduce the depth of recursion for getPart
		// and reduce the solution space.
		dstAST True = new AbsD(var = new VarD(name = 0), a = new AbsD(var =new VarD(name =1), a = new VarD(name = 0)));
		dstAST False =  new AbsD(var = new VarD(name = 0), a = new AbsD(var = new VarD(name = 1), a = new VarD(name = 1)));
		if(??){
			return {| c1|c2|True|False|z|};
		}else if(bnd > 1){
				return 	new ??( a = getPart(c1,c2,bnd-1), 
				                b =getPart(c1,c2,bnd-1), 
				                name = ??,
				                var = new VarD(name = ??));
		}
		
		return null;
	}
	
	switch(s){
		repeat_case:
		{
			dstAST v1 = desugar(s.??);
			dstAST v2 = desugar(s.??);
			return 	new ??(a = getPart(v1, v2, 2),
					       b =getPart(v1, v2, 2), 
					       name = {|s.?? | ?? |},
					       var = getVar(s.??));
		}
	}
}



/**
 * General test harness to exhaustively check for all srcASTs of depth 2.
 */
harness void main1( int[20] arr){
	int idx =0;
	srcAST s = produce( arr, idx,2);
	srcAST c1 = interpretSrcAST(s);
	if(c1!=null){
		dstAST c2  = desugar(s);
		dstAST c3 = interpretDstAST(c2);
		assert(c3!=null);
		assert(equals(c1,c3));
	}
}

/**
 * A concrete harness to assert equivalence of AppS and AppD.
 */
harness  void main2(int str, int str2){
	srcAST s = new AppS(a = new AbsS(var = new VarS(name = str), a= new VarS(name = str)), b = new VarS(name=str2));
	srcAST c1 = interpretSrcAST(s);
	if(c1!=null){
		dstAST c2 = desugar(s);
		dstAST c3 = interpretDstAST(c2);
		VarD c4 = (VarD) c3;
		assert(c4.name == str2);
		assert(c3!=null);
		assert(equals(c1,c3));
	}	
}

/**
 * Interpreter for src language is used by the test harnesses to check the equivalence of the source and destination ASTs 
 */
srcAST interpretSrcAST(srcAST s){
	if(s==null) return null;
	
	switch(s){
		case AppS:
		{
			srcAST s_a = interpretSrcAST(s.a);
			srcAST s_b = s.b;
			if(s_a ==null) return null;
			if(s_b == null) return null;
			switch(s_a){
				case AbsS:{
					srcAST s_new = substituteS(s_a.var, s_a.a, s_b);
					if (s_new == null) return null;
					return interpretSrcAST(s_new);
					
				}
				case AppS:{
					return new AppS(a = s_a, b = s_b);
				}
				case VarS:{
					return new AppS(a = s_a, b= s_b);
				}
				
				default: 
				{
					return null;
				}
			}
		}
		case NotS:
		{
			srcAST s_a = interpretSrcAST(s.a);
			if(s_a==null) return null;
			switch(s_a){
				case FalseS:{ return new TrueS();}
				case TrueS:{ return new FalseS();}
				default: {return null;}
			}
		}
		case AndS:
		{
			bit a, b;
			bit simplified = true;
			srcAST s_a = interpretSrcAST(s.a);
			if(s_a==null) return null;
			switch(s_a){
				case AbsS: return null;
				case AppS: return null;
				case TrueS: a = 1;
				case FalseS: a = 0;
				default: simplified = false;
			}
			srcAST s_b = interpretSrcAST(s.b);
			if(s_b==null) return null;
			switch(s_b){
				case AbsS: return null;
				case AppS: return null;
				case TrueS: b = 1;
				case FalseS: b=0;
				default: simplified = false;
			}
			if(simplified){
				if(a&&b) return new TrueS();
				else return new FalseS();
			}
			else return new AndS( a = s_a, b = s_b);
		}
		case OrS:
		{
			bit a, b;
			bit simplified = true;
			srcAST s_a = interpretSrcAST(s.a);
			if(s_a==null) return null;
			switch(s_a){
				case AbsS: return null;
				case AppS: return null;
				case TrueS: a = 1;
				case FalseS: a = 0;
				default: simplified = false;
			}
			srcAST s_b = interpretSrcAST(s.b);
			if(s_b==null) return null;
			switch(s_b){
				case AbsS: return null;
				case AppS: return null;
				case TrueS: b = 1;
				case FalseS: b=0;
				default: simplified = false;
			}
			if(simplified){
				if(a||b) return new TrueS();
				else return new FalseS();
			}
			else return new OrS(a = s_a, b = s_b);
							
		}
		default: return s;
		}
}

// This variable count is used to generate new variables while substituting below.
// All other variables in this sketch (including generated ones) are guaranteed to have str < 35.
int count = 35;
/**
 * Function that performs the following transformation (λx.E_a) E_b → E_a[E_b /x] on source language.
 */
srcAST substituteS(VarS x, srcAST E_a, srcAST E_b){
	if (E_a ==null || E_b == null || x ==null) return null;
	
	switch(E_a){
		case AppS: return new AppS(a = substituteS(x, E_a.a, E_b), b = substituteS(x, E_a.b, E_b));  
		case AbsS:
		{
			VarS v = E_a.var;
			if(v.name == x.name) {return E_a;}
			else {
			//E_a = Lambda y. E1
				//create a new var z
				VarS z = new VarS(name = count++);
				
				//substitute y in E1 with z
				srcAST e1 = substituteS(v,E_a.a, z);
				
				//Then substitute x with E_b
				srcAST e2 = substituteS(x, e1, E_b);
				
				return new AbsS(var = z, a = e2);
			}
		}
		case VarS:
		{	
			if (E_a.name == x.name) {return E_b;}
			else {return E_a;}
		}
		
		case NotS: return new NotS( a = substituteS(x, E_a.a, E_b));
		case AndS: return new AndS( a = substituteS(x,E_a.a,E_b), b = substituteS(x, E_a.b, E_b));
		case OrS: return new OrS(a = substituteS(x, E_a.a, E_b), b = substituteS(x, E_a.b, E_b));
		default: return E_a;
	}
}

/**
 * Interpreter for dst language 
 */
dstAST interpretDstAST(dstAST s){
	if(s== null) return null;
	switch(s){
		case AppD:
		{
			dstAST s_a = interpretDstAST(s.a);
			dstAST s_b = s.b;
			if(s_a==null||s_b == null) return null;
 			switch(s_a){
				case AbsD:{
					dstAST s_new =  substitute(s_a.var, s_a.a, s_b);
					if(s_new == null) return null;
					
					return interpretDstAST(s_new);
				}
				case AppD: return new AppD(a = s_a, b = s_b);
				case VarD: return new AppD(a = s_a, b= s_b);
				default: return null;
 			}
		}
		default: return s;
	}
	return null;
}

/**
 * Function that performs the following transformation (λx.E_a) E_b → E_a[E_b /x] on destination language.
 */
dstAST substitute(VarD x, dstAST E_a, dstAST E_b){ // E_a[E_b/var]
	if (E_a ==null || E_b == null || x ==null) return null;
	
	switch(E_a){
		
		case AppD: return new AppD(a = substitute(x, E_a.a, E_b), b = substitute(x, E_a.b, E_b));  
		case AbsD: {
			VarD v = E_a.var;
			if(v.name == x.name) {return E_a;}
			else {
				//E_a = Lambda y. E1
				//create a new var z
				VarD z = new VarD(name = count++);
				//substitute y in E1 with z
				dstAST e1 = substitute(v,E_a.a, z);
				
				//Then substitute x with E_b
				dstAST e2 = substitute(x, e1, E_b);
				
				return new AbsD(var = z, a = e2);
			}
		}
		case VarD:
		{	
			if (E_a.name == x.name) { return E_b;}
			else {return E_a;}
		}
	}
	return null;
}

VarS sVar(int name){
	return new VarS(name = name);
}
AbsS sAbs(VarS var, srcAST a){
	return new AbsS(var = var ,a =a);
}
AppS sApp(srcAST a , srcAST b){
	return new AppS(a = a, b = b);
}

/**
 * Function that produces a srcAST from the encoding of maximum depth bnd.
 */
srcAST produce(int[20] encoding, ref int idx, int bnd){
	
	if(bnd <=1 ){ 
		if(encoding[idx] ==0){ idx++; return new VarS(name = encoding[idx++]); }      
        if(encoding[idx]==1){ ++idx; return new FalseS(); }else{ ++idx; return new TrueS(); }    
    }
    if(encoding[idx] ==0){ ++idx; return new VarS(name = encoding[idx++]);}
    else if(encoding[idx]==1){ ++idx; return new FalseS(); }
    else if(encoding[idx] == 2){ ++idx; return new TrueS(); }    
  	else if(encoding[idx]==3){ ++idx; return sApp(produce(encoding, idx, bnd-1), produce(encoding, idx, bnd-1)); }
  	else if(encoding[idx]==4){ ++idx; return sAbs(new VarS(name = encoding[idx++]), produce(encoding, idx, bnd-1)); }
  	else if(encoding[idx]==5){ ++idx; return new AndS(a = produce(encoding, idx, bnd-1), b = produce(encoding, idx, bnd-1)); }
  	else if(encoding[idx]==6){ ++idx; return new OrS(a = produce(encoding, idx, bnd-1), b = produce(encoding,idx, bnd-1)); }
   	else { ++idx; return new NotS( a = produce(encoding, idx, bnd-1)); }
}

/**
 * Checks that two srcAST and dstAST values represent the same value.
 */
bit equals(srcAST c1 ,dstAST c2){
	AbsD True = new AbsD(var = new VarD(name = 32), a = new AbsD(var = new VarD(name =33), a = new VarD(name=32)));
	AbsD False =  new AbsD(var = new VarD(name = 32), a = new AbsD(var = new VarD(name = 33), a = new VarD(name=33)));
	if(c1==null) return true;
	if(c2 == null) return false;
	switch(c1){
		case VarS:
		{
			switch(c2){
				case VarD:return c1.name == c2.name;
				default: return false;
			}
		}
		case AppS:
		{
			switch(c2){
				case AppD: return equals(c1.a, c2.a)  &&  equals(c1.b, c2.b);
				default: return false;
			}
		}
		case AbsS:
		{
			switch(c2){
				case AbsD: {
				VarS sv = c1.var;
				VarD v = new VarD(name = sv.name);
				srcAST e = c1.a;
				switch(e){
					case TrueS: return equals(c1.a, c2.a);
					case FalseS: return equals(c1.a, c2.a);
					default: return equals(c1.a , substitute(c2.var, c2.a, v));
				}
				}
				default: return false;
			}
		}
		case TrueS: return equalsDstAST(True, c2);
		case FalseS: return equalsDstAST(False, c2);
		default: return true;
	}
}

/**
 * Checks that two dstAST values represent the same value.
 * We use this function instead === because of substitions in lambda abstraction constructs.
 */
bit equalsDstAST(dstAST c1, dstAST c2){
	assert(c1!=null);
	if(c2==null) return false;
	switch(c1){
		case AppD:
		{
			switch(c2){
				case AppD: return equalsDstAST(c1.a, c2.a)&& equalsDstAST(c1.b, c2.b) ;
				default: return false;
			}
		}
		case AbsD:
		{
			switch(c2){
				case AbsD: return equalsDstAST(c1.a , substitute(c2.var, c2.a, c1.var));
				default: return false;
			}
		}
		case VarD:
		{
			switch(c2){
				case VarD: return c1.name == c2.name;
				default: return false;
			}
		}
	}
}

